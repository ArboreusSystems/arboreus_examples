QT += quick

CONFIG += c++17

DEFINES += USING_PCH

# Default rules for deployment.
qnx: target.path = /tmp/$${TARGET}/bin
else: unix:!android: target.path = /opt/$${TARGET}/bin
!isEmpty(target.path): INSTALLS += target

INCLUDEPATH += \
	$$PWD

RESOURCES += \
	qml.qrc

PRECOMPILED_HEADER += \
	amainpch.h

HEADERS += \


SOURCES += \
	main.cpp

ios {

	SWIFT_SOURCES +=

	XCODE_SWIFT_VERSION.name = SWIFT_VERSION
	XCODE_SWIFT_VERSION.value = 5.0
	QMAKE_MAC_XCODE_SETTINGS += XCODE_SWIFT_VERSION

	XCODE_SWIFT_OBJC_BRIDGING_HEADER_FILE = $$PWD/aiosswiftbridging.h
	XCODE_SWIFT_OBJC_BRIDGING_HEADER.name = SWIFT_OBJC_BRIDGING_HEADER
	XCODE_SWIFT_OBJC_BRIDGING_HEADER.value = $$XCODE_SWIFT_OBJC_BRIDGING_HEADER_FILE
	QMAKE_MAC_XCODE_SETTINGS += XCODE_SWIFT_OBJC_BRIDGING_HEADER
	OBJECTIVE_HEADERS += $$XCODE_SWIFT_OBJC_BRIDGING_HEADER_FILE

	XCODE_SWIFT_OBJC_INTERFACE_GENERATED_HEADER_NAME = aswiftobjcinterface.h
	XCODE_SWIFT_OBJC_INTERFACE_HEADER_NAME.name = SWIFT_OBJC_INTERFACE_HEADER_NAME
	XCODE_SWIFT_OBJC_INTERFACE_HEADER_NAME.value = $$XCODE_SWIFT_OBJC_INTERFACE_GENERATED_HEADER_NAME
	QMAKE_MAC_XCODE_SETTINGS += XCODE_SWIFT_OBJC_INTERFACE_HEADER_NAME

	XCODE_ENABLE_BITCODE.name = ENABLE_BITCODE
	XCODE_ENABLE_BITCODE.value = NO
	QMAKE_MAC_XCODE_SETTINGS += XCODE_ENABLE_BITCODE

	XCODE_ALWAYS_EMBED_SWIFT_STANDARD_LIBRARIES.name = ALWAYS_EMBED_SWIFT_STANDARD_LIBRARIES
	XCODE_ALWAYS_EMBED_SWIFT_STANDARD_LIBRARIES.value = YES
	QMAKE_MAC_XCODE_SETTINGS += XCODE_ALWAYS_EMBED_SWIFT_STANDARD_LIBRARIES

	SWIFT.input = SWIFT_SOURCES
	SWIFT.variable_out = OBJECT
	SWIFT.output = ${QMAKE_FILE_IN}
	SWIFT.dependency_type = TYPE_C
	QMAKE_EXTRA_COMPILERS += SWIFT
}

macos {

}
